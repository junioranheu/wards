USE [CPFL]
GO
/****** Object:  StoredProcedure [dbo].[SP_FERIADOS_TEMP]    Script Date: 27/11/2023 15:17:20 ******/
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
ALTER PROCEDURE [dbo].[SP_FERIADOS_TEMP]
AS BEGIN
    
	-- Ward: https://dirceuresende.com/blog/como-criar-uma-tabela-com-os-feriados-nacionais-estaduais-e-moveis-no-sql-server/

    ---------------------------------
    -- #1 - Cria tabelas temporárias
    ---------------------------------

    CREATE TABLE #TempFeriados (
        Data DATETIME2 NOT NULL,
        Tipo INT NULL,
        Descricao VARCHAR(100) NOT NULL,
        UF VARCHAR(2) NOT NULL
    );

    CREATE TABLE #TempEstados (
		EstadoId INT,
		Sigla NVARCHAR(2)
	);

	CREATE TABLE #TempDistribuidoras (
		DistribuidoraId INT,
		EstadoId INT
	);

	TRUNCATE TABLE #TempEstados;
	TRUNCATE TABLE #TempFeriados;
	TRUNCATE TABLE #TempDistribuidoras;

	-- Listar estados
	INSERT INTO #TempEstados (EstadoId, Sigla) SELECT EstadoId, Sigla FROM Estados WHERE Status = 1;

	-- Listar distribuidoras
	INSERT INTO #TempDistribuidoras (DistribuidoraId, EstadoId)
	SELECT d.DistribuidoraId, de.EstadoId 
	FROM Distribuidoras d 
	INNER JOIN DistribuidorasEstados de ON de.DistribuidoraId = d.DistribuidoraId;

    ---------------------------------
    -- #2 - Feriados nacionais fixos
    ---------------------------------
 
    INSERT INTO #TempFeriados
    SELECT '0001-01-01 00:00:00', 1,'Confraternização Universal', ''
    UNION
    SELECT '0001-04-21 00:00:00', 1,'Tiradentes', ''
    UNION
    SELECT '0001-05-01 00:00:00', 1,'Dia do Trabalhador', ''
    UNION
    SELECT '0001-09-07 00:00:00', 1,'Independência do Brasil', ''
    UNION
    SELECT '0001-10-12 00:00:00', 1,'Nossa Senhora Aparecida', ''
    UNION
    SELECT '0001-11-02 00:00:00', 1,'Finados', ''
    UNION
    SELECT '0001-11-15 00:00:00', 1,'Proclamação da República', ''
    UNION
    SELECT '0001-12-25 00:00:00', 1,'Natal', ''
 
    ---------------------------------
    -- #3 - Feriados estaduais fixos
    ---------------------------------
 
    -- Acre
    INSERT INTO #TempFeriados
    SELECT '0001-06-15 00:00:00', 2,'Aniversário do estado', 'AC'
    UNION
    SELECT '0001-09-05 00:00:00', 2,'Dia da Amazônia', 'AC'
    UNION
    SELECT '0001-11-17 00:00:00', 2,'Assinatura do Tratado de Petrópolis', 'AC'
 
    -- Alagoas
    INSERT INTO #TempFeriados
    SELECT '0001-06-24 00:00:00', 2,'São João', 'AL'
    UNION
    SELECT '0001-06-29 00:00:00', 2,'São Pedro', 'AL'
    UNION
    SELECT '0001-09-16 00:00:00', 2,'Emancipação política', 'AL'
    UNION
    SELECT '0001-11-20 00:00:00', 2,'Morte de Zumbi dos Palmares', 'AL'
 
    -- Amapá
    INSERT INTO #TempFeriados
    SELECT '0001-03-19 00:00:00', 2,'Dia de São José, santo padroeiro do Estado do Amapá', 'AP'
    UNION
    SELECT '0001-09-13 00:00:00', 2,'Criação do Território Federal (Data Magna do estado)', 'AP'
    UNION
    SELECT '0001-11-20 00:00:00', 2,'Dia da Consciência Negra', 'AP'
 
    -- Amazonas
    INSERT INTO #TempFeriados
    SELECT '0001-09-05 00:00:00', 2,'Elevação do Amazonas à categoria de província', 'AM'
    UNION
    SELECT '0001-11-20 00:00:00', 2,'Dia da Consciência Negra', 'AM'
 
    -- Bahia
    INSERT INTO #TempFeriados
    SELECT '0001-07-02 00:00:00', 2,'Independência da Bahia (Data magna do estado)', 'BA'
 
    -- Ceará
    INSERT INTO #TempFeriados
    SELECT '0001-03-25 00:00:00', 2,'Data magna do estado (data da abolição da escravidão no Ceará)', 'CE'
 
    -- Distrito Federal
    INSERT INTO #TempFeriados
    SELECT '0001-04-21 00:00:00', 2,'Fundação de Brasília', 'DF'
    UNION
    SELECT '0001-11-30 00:00:00', 2,'Dia do evangélico', 'DF'
 
    -- Maranhão
    INSERT INTO #TempFeriados
    SELECT '0001-07-28 00:00:00', 2,'Adesão do Maranhão à independência do Brasil', 'MA'
 
    -- Mato Grosso
    INSERT INTO #TempFeriados
    SELECT '0001-11-20 00:00:00', 2,'Dia da Consciência Negra', 'MT'
 
    -- Mato Grosso do Sul
    INSERT INTO #TempFeriados
    SELECT '0001-10-11 00:00:00', 2,'Criação do estado', 'MS'
 
    -- Minas Gerais
    INSERT INTO #TempFeriados
    SELECT '0001-04-21 00:00:00', 2,'Data magna do estado', 'MG'
 
    -- Pará
    INSERT INTO #TempFeriados
    SELECT '0001-08-15 00:00:00', 2,'Adesão do Grão-Pará à independência do Brasil (data magna)', 'PA'
 
    -- Paraíba
    INSERT INTO #TempFeriados
    SELECT '0001-08-05 00:00:00', 2,'Fundação do Estado em 1585', 'PB'
 
    -- Piauí
    INSERT INTO #TempFeriados
    SELECT '0001-10-19 00:00:00', 2,'Dia do Piauí', 'PI'
 
    -- Rio de Janeiro
    INSERT INTO #TempFeriados
    SELECT '0001-04-23 00:00:00', 2,'Dia de São Jorge', 'RJ'
    UNION
    SELECT '0001-11-20 00:00:00', 2,'Dia da Consciência Negra', 'RJ'
 
    -- Rio Grande do Norte
    INSERT INTO #TempFeriados
    SELECT '0001-10-03 00:00:00', 2,'Mártires de Cunhaú e Uruaçu', 'RN'
 
    -- Rio Grande do Sul
    INSERT INTO #TempFeriados
    SELECT '0001-09-20 00:00:00', 2,'Proclamação da República Rio-Grandense', 'RS'
 
    -- Rondônia
    INSERT INTO #TempFeriados
    SELECT '0001-10-04 00:00:00', 2,'Criação do estado', 'RO'
	UNION
	SELECT '0001-06-18 00:00:00', 2,'Dia do evangélico', 'RO'

    -- Roraima
    INSERT INTO #TempFeriados
    SELECT '0001-10-05 00:00:00', 2,'Criação do estado', 'RR'
 
    -- São Paulo
    INSERT INTO #TempFeriados
    SELECT '0001-07-09 00:00:00', 2,'Revolução Constitucionalista de 1932', 'SP'
	UNION
    SELECT '0001-11-20 00:00:00', 2,'Dia da Consciência Negra', 'SP'
 
    -- Sergipe
    INSERT INTO #TempFeriados
    SELECT '0001-07-08 00:00:00', 2,'Aniversário de Sergipe', 'SE'
 
    -- Tocantins
    INSERT INTO #TempFeriados
    SELECT '0001-10-05 00:00:00', 2,'Criação do estado', 'TO'
 
    ---------------------------------
    -- #4 - Feriados móveis
    ---------------------------------
 
    DECLARE
        @ano INT,
        @seculo INT,
        @G INT,
        @K INT,
        @I INT,
        @H INT,
        @J INT,
        @L INT,
        @MesDePascoa INT,
        @DiaDePascoa INT,
        @pascoa DATETIME 
 
    DECLARE 
        @Dt_Inicial DATETIME = '2019-01-01',
        @Dt_Final DATETIME = '2033-01-01'
 
    WHILE(@Dt_Inicial <= @Dt_Final)
    BEGIN
        
        -- Calcula dinamicamente as datas para feriados móveis;    
        SET @ano = YEAR(@Dt_Inicial)
 
        SET @seculo = @ano / 100 
        SET @G = @ano % 19
        SET @K = ( @seculo - 17 ) / 25
        SET @I = ( @seculo - CAST(@seculo / 4 AS int) - CAST(( @seculo - @K ) / 3 AS int) + 19 * @G + 15 ) % 30
        SET @H = @I - CAST(@I / 28 AS int) * ( 1 * -CAST(@I / 28 AS int) * CAST(29 / ( @I + 1 ) AS int) ) * CAST(( ( 21 - @G ) / 11 ) AS int)
        SET @J = ( @ano + CAST(@ano / 4 AS int) + @H + 2 - @seculo + CAST(@seculo / 4 AS int) ) % 7
        SET @L = @H - @J
        SET @MesDePascoa = 3 + CAST(( @L + 40 ) / 44 AS int)
        SET @DiaDePascoa = @L + 28 - 31 * CAST(( @MesDePascoa / 4 ) AS int)
        SET @pascoa = CONVERT(DATETIME, CONVERT(VARCHAR(2), @DiaDePascoa) + '/' + CONVERT(VARCHAR(2), @MesDePascoa) + '/' + CONVERT(VARCHAR(4), @ano), 103) + '00:00:00'
 
        -- Insere os feriados móveis;    
        INSERT INTO #TempFeriados
        SELECT @pascoa, 1,'Paixão de Cristo', ''
        
        -- INSERT INTO #TempFeriados
        -- SELECT DATEADD(DAY, -48, @pascoa), 1,'Carnaval', ''
        
        INSERT INTO #TempFeriados
        SELECT DATEADD(DAY, -47, @pascoa), 1,'Carnaval', ''
        
        INSERT INTO #TempFeriados
        SELECT DATEADD(DAY, 60, @pascoa), 1,'Corpus Christi', ''
 
        SET @Dt_Inicial = DATEADD(YEAR, 1, @Dt_Inicial)
    END

    ---------------------------------
    -- #5 - Limpar tabelas e iterar tabela temporária
    ---------------------------------

	-- 
	DELETE FROM FeriadosDatas;
	DELETE FROM FeriadosDistribuidoras;
	DELETE FROM FeriadosEstados;
	DELETE FROM Feriados;

	-- 
    DECLARE @F_DATA DATETIME2;
    DECLARE @F_TIPO INT;
    DECLARE @F_NOME VARCHAR(100);
    DECLARE @F_UF VARCHAR(2);
	DECLARE @IS_MOVEL BIT;

	DECLARE @ID_CHECK_IS_FERIADO_JA_CADASTRADO INT;
	DECLARE @ID_ULTIMO_FERIADO_CRIADO INT;

	DECLARE @IS_EXISTE_ESTADO INT;
	DECLARE @E_ESTADO_ID INT;

	DECLARE @IS_EXISTE_DISTRIBUIDORA INT;
	DECLARE @D_DISTRIBUIDORA_ID INT;

    -- Declarar cursor para loop
    DECLARE temp_cursor_feriado CURSOR FOR
    SELECT Data, Tipo, Descricao, UF FROM #TempFeriados;

    OPEN temp_cursor_feriado;

    -- Fetch
    FETCH NEXT FROM temp_cursor_feriado INTO @F_DATA, @F_TIPO, @F_NOME, @F_UF;

    -- Loop
    WHILE @@FETCH_STATUS = 0
    BEGIN
	
		-- Verificar se o feriado já foi cadastrado para evitar duplicidade...
		-- Vários carnavais com varias datas ❌❌❌
		-- 1 carnaval com multiplas datas ✅✅✅
		SET @ID_CHECK_IS_FERIADO_JA_CADASTRADO = (SELECT TOP 1 FeriadoId FROM dbo.Feriados WHERE Nome = @F_NOME);

		SET @IS_MOVEL = (SELECT CASE
			WHEN @F_DATA LIKE '%0001%' THEN 0
			ELSE 1
		END AS Result);

		IF @ID_CHECK_IS_FERIADO_JA_CADASTRADO IS NULL
		BEGIN
		    -- #5.1.1 - Caso o feriado não exista, inserir dados na tabela dbo.Feriados e atualize a variável para utilizar nos outros inserts com o id mais recente
			INSERT INTO dbo.Feriados (Tipo, Nome, IsMovel, Status, DataCriacao, DataAtualizacao, UsuarioId, UsuarioIdMod)
			VALUES (@F_TIPO, @F_NOME, @IS_MOVEL, 1, GETDATE(), NULL, 1, NULL);

			-- Atribuir à uma variável o último ID criado;
			SET @ID_ULTIMO_FERIADO_CRIADO = SCOPE_IDENTITY();
		END ELSE
		BEGIN
		    -- #5.1.2 - Caso o feriado já exista, atualize a variável para utilizar nos outros inserts
			SET @ID_ULTIMO_FERIADO_CRIADO = @ID_CHECK_IS_FERIADO_JA_CADASTRADO;
		END

		-- #5.2 - Inserir dados na tabela dbo.FeriadosDatas
		INSERT INTO dbo.FeriadosDatas (Data, FeriadoId)
        VALUES (@F_DATA, @ID_ULTIMO_FERIADO_CRIADO);

		-- #5.3 - Inserir dados na tabela dbo.FeriadosEstados
		IF @F_TIPO = 1 -- nacional
		BEGIN
			-- Se o feriado for nacional, insira apenas em seu estado
			-- Declarar cursor para loop dos estados
			DECLARE temp_cursor_estado CURSOR FOR
			SELECT EstadoId FROM #TempEstados;

			OPEN temp_cursor_estado;

			-- Fetch
			FETCH NEXT FROM temp_cursor_estado INTO @E_ESTADO_ID;

			WHILE @@FETCH_STATUS = 0
			BEGIN
				SET @IS_EXISTE_ESTADO = (SELECT DISTINCT 1 FeriadoEstadoId FROM FeriadosEstados WHERE EstadoId = @E_ESTADO_ID AND FeriadoId = @ID_ULTIMO_FERIADO_CRIADO);

				IF @IS_EXISTE_ESTADO IS NULL
				BEGIN
					-- Se o feriado for nacional, insira em todos os estados
					INSERT INTO dbo.FeriadosEstados (EstadoId, FeriadoId)
					VALUES (@E_ESTADO_ID, @ID_ULTIMO_FERIADO_CRIADO);
				END

					-- Fetch next
				FETCH NEXT FROM temp_cursor_estado INTO @E_ESTADO_ID;
			END

			CLOSE temp_cursor_estado;
			DEALLOCATE temp_cursor_estado;
		END ELSE
		BEGIN
			-- Se o feriado for estadual, insira apenas em seu estado
			SET @E_ESTADO_ID = (SELECT TOP 1 EstadoId FROM #TempEstados WHERE Sigla = @F_UF);

			INSERT INTO dbo.FeriadosEstados (EstadoId, FeriadoId)
			VALUES (@E_ESTADO_ID, @ID_ULTIMO_FERIADO_CRIADO);
		END

		-- #5.4 - Inserir dados na tabela dbo.FeriadosDistribuidoras
		IF @F_TIPO = 1 -- nacional
		BEGIN
			-- Se o feriado for nacional, insira todas as distribuidoras no feriado
			-- Declarar cursor para loop dos estados
			DECLARE temp_cursor_distribuidora CURSOR FOR
			SELECT DistribuidoraId FROM #TempDistribuidoras;

			OPEN temp_cursor_distribuidora;

			-- Fetch
			FETCH NEXT FROM temp_cursor_distribuidora INTO @D_DISTRIBUIDORA_ID;

			WHILE @@FETCH_STATUS = 0
			BEGIN
				SET @IS_EXISTE_DISTRIBUIDORA = (SELECT DISTINCT 1 FeriadoDistribuidoraId FROM FeriadosDistribuidoras WHERE DistribuidoraId = @D_DISTRIBUIDORA_ID AND FeriadoId = @ID_ULTIMO_FERIADO_CRIADO);

				IF @IS_EXISTE_DISTRIBUIDORA IS NULL
				BEGIN
					-- Se o feriado for nacional, insira em todos os estados
					INSERT INTO dbo.FeriadosDistribuidoras (DistribuidoraId, FeriadoId)
					VALUES (@D_DISTRIBUIDORA_ID, @ID_ULTIMO_FERIADO_CRIADO);
				END

					-- Fetch next
				FETCH NEXT FROM temp_cursor_distribuidora INTO @D_DISTRIBUIDORA_ID;
			END

			CLOSE temp_cursor_distribuidora;
			DEALLOCATE temp_cursor_distribuidora;
		END ELSE
		BEGIN
			-- Se o feriado for estadual, insira apenas algumas distribuidoras em alguns estados
			-- Declarar cursor para loop dos estados
			SET @E_ESTADO_ID = (SELECT TOP 1 EstadoId FROM #TempEstados WHERE Sigla = @F_UF);

			DECLARE temp_cursor_distribuidora CURSOR FOR
			SELECT DistribuidoraId FROM #TempDistribuidoras WHERE EstadoId = @E_ESTADO_ID;

			OPEN temp_cursor_distribuidora;

			-- Fetch
			FETCH NEXT FROM temp_cursor_distribuidora INTO @D_DISTRIBUIDORA_ID;

			WHILE @@FETCH_STATUS = 0
			BEGIN
				-- Se o feriado for nacional, insira em todos os estados
				INSERT INTO dbo.FeriadosDistribuidoras (DistribuidoraId, FeriadoId)
				VALUES (@D_DISTRIBUIDORA_ID, @ID_ULTIMO_FERIADO_CRIADO);

					-- Fetch next
				FETCH NEXT FROM temp_cursor_distribuidora INTO @D_DISTRIBUIDORA_ID;
			END

			CLOSE temp_cursor_distribuidora;
			DEALLOCATE temp_cursor_distribuidora;
		END

        -- Fetch next
        FETCH NEXT FROM temp_cursor_feriado INTO @F_DATA, @F_TIPO, @F_NOME, @F_UF;
    END

	---------------------------------
    -- #6 - Garbage collector
    ---------------------------------
    CLOSE temp_cursor_feriado;
    DEALLOCATE temp_cursor_feriado;

	-- SELECT * FROM #TempFeriados;
	DROP TABLE #TempFeriados;
	DROP TABLE #TempEstados;
	DROP TABLE #TempDistribuidoras;
    
END